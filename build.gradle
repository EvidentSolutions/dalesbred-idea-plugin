buildscript {
    ext.kotlin_version = '1.0.6'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.0"
}

apply plugin: 'kotlin'

intellij {
    version 'IU-2016.3'
    pluginName 'Dalesbred'
    updateSinceUntilBuild false

    publish {
        pluginId = '7313'

        if (findProperty('ideaPublishUsername') != null) {
            username = ideaPublishUsername
            password = ideaPublishPassword
        }
    }
}

group 'fi.evident.dalesbred.plugin.idea'
version '0.5.1'

configurations {
    testLibs
}

repositories {
    jcenter()
}

dependencies {
    testLibs 'fi.evident.dalesbred:dalesbred:0.8.0'
    testLibs 'org.dalesbred:dalesbred:1.2.2'
}

task downloadTestLibs(type: Copy) {
    from configurations.testLibs
    into 'libs/'
}

test.dependsOn downloadTestLibs
